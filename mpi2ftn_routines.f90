!
! mpi2ftn_routines.f90
!
! Convert matelem files generated by MPI-TROVE to old, non-MPIIO format.
!
! Designed to be used with MMAP to handle larger-than-RAM files.
! Intel compilers can't handle this (will still buffer file), so using allocated arrays instead - faster than MMAP in this case.
!
module mpi2ftn_routines
  use iso_c_binding
  use accuracy
!#ifdef __INTEL_COMPILER
!  use ifport, only: ftelli8
!#endif

  implicit none

  ! Default to private
  private

  public convert_matelem

  interface

    type(c_ptr) function do_mmap(filename) bind(c,name='mmap_file_rdonly')
      use iso_c_binding
      character(c_char) :: filename
    end function do_mmap

    integer(c_int64_t) function get_mmapped_file_length(filename) bind(c,name='get_mmapped_file_length')
      use iso_c_binding
      character(c_char) :: filename
    end function get_mmapped_file_length

    integer(c_int) function munmap(addr, length) bind(c,name='munmap')
      use iso_c_binding
      type(c_ptr)         :: addr
      integer(c_int64_t)  :: length
    end function munmap

  end interface


  contains


    subroutine mmap_file(filename, handle)
      character(len=*), intent(in)  :: filename
      type(c_ptr), intent(out)      :: handle

      handle = do_mmap(filename)
    end subroutine mmap_file

!#ifdef __INTEL_COMPILER
!    ! ftell 'just works' with GNU, Intel needs some imports and specific i8 routine...
!    function ftell(handle) result(pos)
!      integer       :: handle
!      integer(hik)  :: pos
!
!      pos = ftelli8(handle)
!    end function ftell
!#endif

    ! Structure stole form restore_rot_kinetic_matrix_elements in trove/tran.f90
    subroutine convert_matelem()

      integer(ik)        :: islice
      character(len=25)  :: buf
      character(len=35)  :: filename
      integer(ik)        :: ncontr_t
      integer(ik) :: ierr

      integer(ik) :: Maxsymcoeffs, max_deg_size, Maxcontracts, nclasses
      integer(ik) :: iounit = 1000, matelem_in = 1001, matelem_out = 1002
      integer(ik) :: verbose = 7


      integer(selected_int_kind(16))  :: filesize
      integer(hik)  :: offset
      integer(hik)  :: readsize

      type(c_ptr)   :: matelem_mapped_c
      character(len=1), pointer,dimension(:)  :: matelem_mapped

      ! Intel has issues with mmap...
      real(rk),dimension(:,:),allocatable     :: matelem
      integer(ik),dimension(:,:),allocatable  :: contr_dat

      !!Â Get some metadata
      open(unit = iounit, action = 'read',status='old', file = "eigen_quanta0.chk" )

      read(iounit, '(a)') buf(1:25)
      if (buf(1:25) /= 'Start Primitive basis set') stop 'eigen_quanta0.chk - invalid header'


      read(iounit, '(4i8)') Maxsymcoeffs, max_deg_size, Maxcontracts, nclasses

      write(*,*) Maxsymcoeffs, max_deg_size, Maxcontracts, nclasses
      !! END Get some metadata

      ! Intel has issues with mmap...
#ifndef __INTEL_COMPILER
      !! MMAP the main contr_matelem
      filename = "contr_matelem.chk"//char(0)
      filesize = get_mmapped_file_length(filename)
      call mmap_file(filename, matelem_mapped_c)
      call c_f_pointer(matelem_mapped_c, matelem_mapped, [filesize])
      !! END MMAP the main contr_matelem
#endif

      open(unit = matelem_in, action = 'read', status = 'old', access='stream', file = 'contr_matelem.chk')
      open(unit = matelem_out, action = 'write', status = 'replace', position = 'rewind', form = 'unformatted', &
        & file = 'contr_matelem.chk.oldfmt')

      read(matelem_in) buf(1:7)
      if (buf(1:7)/='[MPIIO]') stop "contr_matelem.chk - MPIIO header missing"

      read(matelem_in) buf(1:18)
      if (buf(1:18)/='Start Kinetic part') stop "contr_matelem.chk - corrupt header"
      write(matelem_out) buf(1:18)

      read(matelem_in) ncontr_t
      if (Maxcontracts/=ncontr_t) stop "contr_matelem.chk - ncontr_t != Maxcontracts"
      write(matelem_out) ncontr_t

      read(matelem_in) buf(1:10)
      if (buf(1:10)/='icontr_cnu') stop "contr_matelem.chk - expected 'icontr_cnu', found garbage"
      write(matelem_out) buf(1:10)

      ! Intel has issues with mmap...
#ifndef __INTEL_COMPILER
      offset = ftell(matelem_in)

      write(matelem_out) matelem_mapped(offset+1:offset+ik*((1+nclasses)*ncontr_t))

      call fseek(matelem_in, ik*((1+nclasses)*ncontr_t), 1, ierr)
#else
      allocate(contr_dat(1+nclasses,ncontr_t), stat=ierr)
      read(matelem_in) contr_dat
      write(matelem_out) contr_dat
#endif

      read(matelem_in) buf(1:11)
      if (buf(1:11)/='icontr_ideg') stop "contr_matelem.chk - expected 'icontr_ideg', found garbage"
      write(matelem_out) buf(1:11)

      ! Intel has issues with mmap...
#ifndef __INTEL_COMPILER
      offset = ftell(matelem_in)

      write(matelem_out) matelem_mapped(offset+1:offset+ik*((1+nclasses)*ncontr_t))

      call fseek(matelem_in, ik*((1+nclasses)*ncontr_t), 1, ierr)
#else
      read(matelem_in) contr_dat
      write(matelem_out) contr_dat
      deallocate(contr_dat, stat=ierr)
#endif

      !hvib tag
      read(matelem_in) buf(1:4)
      write(matelem_out) buf(1:4)

      !hvib data
      ! Intel has issues with mmap...
#ifndef __INTEL_COMPILER
      offset = ftell(matelem_in)

      readsize = int(rk, hik)
      readsize = readsize * ncontr_t * ncontr_t

      write(out,*) "Matrix size in bytes: ", readsize

      write(matelem_out) matelem_mapped(offset+1:offset+(readsize))

      call fseek(matelem_in, readsize, 1, ierr)
#else
      allocate(matelem(ncontr_t,ncontr_t), stat=ierr)
      read(matelem_in) matelem
      write(matelem_out) matelem
      deallocate(matelem, stat=ierr)
#endif

      read(matelem_in) buf(1:16)
      if (buf(1:16)/='End Kinetic part') stop "contr_matelem.chk - corrupt footer"
      write(matelem_out) buf(1:16)

      close(matelem_out)
      close(matelem_in)
#ifndef __INTEL_COMPILER
      ierr = munmap(matelem_mapped_c, filesize)
      nullify(matelem_mapped)
#endif

      write(*,*) "Converting slices..."

      ! NOTE: these islice numbers are hard-coded for H2CO-1.
      ! Appears to be fine for g_rot/g_cor, may need fixing for extF
      do islice=1,9 !g_rot
        call conv_slice('matelem', islice, 'g_rot', ncontr_t)
      end do
      do islice=10,12 !g_cor
        call conv_slice('matelem', islice, 'g_cor', ncontr_t)
      end do
      !do islice=1,3 !extF
      !  call conv_slice('extmatelem', islice, 'extF', ncontr_t)
      !end do

    contains

      subroutine conv_slice(prefix, num, tag, ncontr)
        character(len=*), intent(in)            :: prefix
        character(len=*), intent(in)            :: tag
        integer(ik), intent(in)                 :: num
        integer(ik), intent(in)                 :: ncontr

        integer(hik)                            :: writesize
        integer(hik)                            :: filesize
        integer(hik)                            :: offset


        integer                                 :: slice_in = 1100, slice_out = 1102
        type(c_ptr)                             :: slice_mapped_c
        character(len=1), pointer,dimension(:)  :: slice_mapped

        character(len=30)                       :: filename_in
        character(len=30)                       :: filename_out
        character(len=2)                        :: num_str
        character(len=5)                        :: tagbuf

        integer                                 :: taglength
        integer                                 :: ierr

        ! Intel has issues with mmap...
        real(rk),dimension(:,:),allocatable     :: matelem

        write(num_str, '(i2)') num
        filename_in = prefix//trim(adjustl(num_str))//".chk"//char(0)
        filename_out = prefix//trim(adjustl(num_str))//".chk.oldfmt"

        ! Intel has issues with mmap...
#ifndef __INTEL_COMPILER
        filesize = get_mmapped_file_length(filename_in)
        call mmap_file(filename_in, slice_mapped_c)
        call c_f_pointer(slice_mapped_c, slice_mapped, [filesize])
#endif

        taglength = len(tag)

        open(unit = slice_in, action = 'read', status = 'old', access='stream', file = filename_in)
        open(unit = slice_out, action = 'write', status = 'replace', position = 'rewind', form = 'unformatted', file = filename_out)

        write(*,*) "Converting slice: ", filename_in

        read(slice_in) tagbuf(1:taglength)
        write(slice_out) tagbuf(1:taglength)

        ! Intel has issues with mmap...
#ifndef __INTEL_COMPILER
        offset = ftell(slice_in)

        writesize = int(rk, hik)
        writesize = writesize * ncontr * ncontr

        write(slice_out) slice_mapped(offset+1:offset+writesize)

        call fseek(slice_in, writesize, 1, ierr)
#else
        allocate(matelem(ncontr_t,ncontr_t), stat=ierr)
        read(slice_in) matelem
        write(slice_out) matelem
        deallocate(matelem, stat=ierr)
#endif

        read(slice_in) tagbuf(1:taglength)
        if (tagbuf(1:taglength)/=tag) write(*,*) "Closing tag incorrect: ", tagbuf(1:taglength), " but expected ", tag
        write(slice_out) tagbuf(1:taglength)

        close(slice_out)
        close(slice_in)
#ifndef __INTEL_COMPILER
        !ierr = munmap(slice_mapped_c, filesize)
        !nullify(slice_mapped)
#endif

      end subroutine conv_slice

    end subroutine convert_matelem
end module mpi2ftn_routines
